plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'xyz.jpenilla.run-paper' version '1.0.6'
}

group = 'me.lorenzo0111'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'com.github.CraftingStore.MinecraftPlugin:bukkit:master-SNAPSHOT'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set("")

    relocate("org.apache.commons.codec", "me.lorenzo0111.analytics.http.codec")
    relocate("okhttp3", "me.lorenzo0111.analytics.http.request")
}

runServer {
    minecraftVersion('1.18.2')
}

tasks.build.dependsOn tasks.shadowJar